apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "uk.digitalsquid.netspoofer"
        minSdkVersion 10
        targetSdkVersion 19
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main.jni.srcDirs = []
}

dependencies {
    compile fileTree(dir: new File(buildDir, 'libs'), include: '*.jar')

    compile project(':androidsupportv4preferencefragment')
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.google.android.gms:play-services:+'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkLibsToJar,ndkBinariesToJar
}

task ndkLibsToJar(type: Zip, dependsOn: 'ndkBuild', description: 'Create a JAR of the native libs') {
    destinationDir new File(buildDir, 'libs')
    baseName 'ndk-libs'
    extension 'jar'
    from(new File(buildDir, 'libs')) {
        include '**/*.so'
    }
    into 'lib/'
}

task ndkBinariesToJar(type: Zip, dependsOn: 'ndkBuild', description: 'Create a JAR of the native binaries') {
    destinationDir new File(buildDir, 'libs')
    baseName 'ndk-binaries'
    extension 'jar'
    from(new File(buildDir, 'libs')) {
        include '**/arp-scan'
        include '**/arpspoof'
    }
    into 'assets/binaries/'
}

task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
    def ndkDir = project.plugins.findPlugin('com.android.application').getNdkFolder()
    println(ndkDir)
    commandLine "$ndkDir/ndk-build",
            'NDK_PROJECT_PATH=build',
            'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
            'NDK_APPLICATION_MK=src/main/jni/Application.mk'
}